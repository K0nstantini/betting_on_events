#pragma version >=0.4.0;

#include "imports/stdlib.fc";
#include "imports/errors.fc";
#include "imports/messages.fc";
#include "imports/op-codes.fc";
#include "imports/constants.fc";

const deposit_required_gas = 15000000; ;; 0.015 TON
const max_deposit_gas = 1000000; ;; 0.1 TON
const withdraw_required_gas = 10000000; ;; 0.01 TON

slice load_data() inline {
    slice ds = get_data().begin_parse();
    return ds~load_msg_addr();
}

() save_data(slice address) impure inline {
    set_data(begin_cell()
            .store_slice(address)
            .end_cell()
    );
}

() on_bounce (slice in_msg_body) impure {
    in_msg_body~skip_bits(32);
    int op = in_msg_body~load_uint(32);
    throw_unless(error::invalid_op_code(), op == op::deposit_ton());
    int query_id = in_msg_body~load_uint(64);
    slice to_addr = in_msg_body~load_msg_addr();
    int ton_amount = in_msg_body~load_coins();
    messages::send_empty(to_addr, send_mode::paid_externally(), ton_amount);
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) {
        return ();
    }

    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    if (flags & 1) {
        on_bounce(in_msg_body);
        return ();
    }

    slice sender_addr = cs~load_msg_addr();
    int op = in_msg_body~load_uint(32);
    int query_id = in_msg_body~load_uint(64);

    int ton_balance_before_msg = my_balance - msg_value;
    msg_value -= const::min_tons_for_storage() - min(ton_balance_before_msg, const::min_tons_for_storage());

    slice owner = load_data();

    if (op == op::deposit_ton()) {
        ;; balance = 0.55
        ;; msg_value = 0.5
        int ton_amount = in_msg_body~load_coins(); ;; 0.45
        int required_value = ton_amount + deposit_required_gas; ;; 0.45 + 0.015 = 0.465
        throw_unless(error::not_enough_gas_amount(), msg_value > required_value); ;; 0.5 >= 0.465
        ton_amount = max(ton_amount, msg_value - max_deposit_gas); ;; max(0.45, 0.5 - 0.1) = 0.45
        int send_value = msg_value - required_value; ;; 0.5 - 0.465 = 0.035
        int ton_supply = my_balance - const::min_tons_for_storage() - deposit_required_gas - send_value; ;; 0.55 - 0.05 - 0.015 - 0.035 = 0.45

        var msg_body = begin_cell()
                .store_uint(op::deposit_ton(), 32)
                .store_uint(query_id, 64)
                .store_slice(sender_addr)
                .store_coins(ton_amount)
                .store_coins(ton_supply)
                .end_cell();

        messages::send_simple(owner, msg_body, send_mode::paid_externally(), send_value);
        return ();
    }

    if (op == op::withdraw_ton()) {
        throw_unless(error::not_enough_gas_amount(), msg_value >= withdraw_required_gas);
        throw_unless(error::invalid_sender_addr(), equal_slices(sender_addr, owner));

        slice to_addr = in_msg_body~load_msg_addr();
        int amount = in_msg_body~load_coins();

        messages::send_empty(to_addr, send_mode::paid_externally(), amount);
        return ();
    }

    if (op == op::change_addr()) {
        throw_unless(error::invalid_sender_addr(), equal_slices(sender_addr, owner));
        slice new_owner_addr = in_msg_body~load_msg_addr();
        save_data(new_owner_addr);
        return ();
    }

    throw(0xffff);
}

slice get_vault_data() method_id {
    return load_data();
}
