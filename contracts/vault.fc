#pragma version >=0.4.0;

#include "imports/stdlib.fc";
#include "imports/errors.fc";
#include "imports/messages.fc";
#include "imports/op-codes.fc";
#include "imports/constants.fc";

const gas_consumption = 10000000; ;; 0.01 TON
const min_deposit = 1000000; ;; 0.001 TON (1 BET)
const deposit_send_value = 45000000; ;; 0.045 TON

slice load_data() inline {
    slice ds = get_data().begin_parse();
    return ds~load_msg_addr();
}

() save_data(slice address) impure inline {
    set_data(begin_cell()
            .store_slice(address)
            .end_cell()
    );
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) {
        return ();
    }

    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    if (flags & 1) {
        return ();
    }

    slice sender_addr = cs~load_msg_addr();
    int op = in_msg_body~load_uint(32);
    int query_id = in_msg_body~load_uint(64);


    slice owner = load_data();

    if (op == op::deposit_ton()) {
        int ton_balance_before_msg = my_balance - msg_value;
        msg_value -= const::min_tons_for_storage() - min(ton_balance_before_msg, const::min_tons_for_storage());

        int required_value = gas_consumption + deposit_send_value;
        throw_unless(error::not_enough_gas_amount(), msg_value >= required_value + min_deposit);

        var msg_body = begin_cell()
                .store_uint(op::deposit_ton(), 32)
                .store_uint(query_id, 64)
                .store_slice(sender_addr)
                .store_coins(msg_value - required_value)
                .store_coins(my_balance - const::max_tons_for_storage() - required_value)
                .end_cell();

        messages::send_simple(owner, msg_body, send_mode::paid_externally(), deposit_send_value);
        return ();
    }

    if (op == op::withdraw_ton()) {
        throw_unless(error::invalid_sender_addr(), equal_slices(sender_addr, owner));
        throw_unless(error::not_enough_gas_amount(), msg_value >= gas_consumption);

        slice to_addr = in_msg_body~load_msg_addr();
        int amount = in_msg_body~load_coins();

        messages::send_empty(to_addr, send_mode::paid_externally(), amount);
        return ();
    }

    if (op == op::change_addr()) {
        throw_unless(error::invalid_sender_addr(), equal_slices(sender_addr, owner));
        slice new_owner_addr = in_msg_body~load_msg_addr();
        save_data(new_owner_addr);
        return ();
    }

    throw(0xffff);
}

slice get_vault_data() method_id {
    return load_data();
}
